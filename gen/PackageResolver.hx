import Types.JsonApi;

class PackageResolver {
  final paths: Map<String, String>;

  final version: String;

  public function new(api: JsonApi) {
    paths = buildPackagePaths(api);
    version = api.version;
  }

  public function getImportLines(types: Map<String, Bool>, pkgName: String): Array<String> {
    var imports: Array<String> = [];
    for (type in types.keys()) {
      final module = paths[type];
      if (module != null && module != pkgName) {
        imports.push('import ${module}.${type};');
      }
    }

    return imports;
  }

  public function createPackageLine(packageName: String): Array<String> {
    return [
      '// Generated by https://github.com/jurmerlo/love2d-hx.',
      '// Love2D Version ${version}.',
      '',
      'package ${packageName};',
      ''
    ];
  }

  function buildPackagePaths(api: JsonApi): Map<String, String> {
    final lovePaths: Map<String, String> = new Map();

    for (type in api.types) {
      lovePaths[type.name] = 'love';
    }

    for (module in api.modules) {
      var moduleName = 'love.${module.name}';
      if (module.types?.length > 0) {
        for (type in module.types) {
          lovePaths[type.name] = moduleName;
        }
      }

      if (module.enums?.length > 0) {
        for (type in module.enums) {
          lovePaths[type.name] = moduleName;
        }
      }
    }

    return lovePaths;
  }
}
